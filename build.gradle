ext {
    // The drivers we want to use
    drivers = ["firefox", "chrome", "phantomJs","ie"]
    groovyVersion = '2.4.7'
    gebVersion = '1.0'
    spockVersion = '1.1-groovy-2.4-rc-1'
    seleniumVersion = '3.0.1'
    chromeDriverVersion = '2.27'
    geckoDriverVersion = '0.13.0'
    ieDriverVersion = '3.0.0'
    ieDriverMajorVersion = '3.0'
    phantomJsVersion = '2.1.1'
    yamlVersion = '1.17'
}

apply plugin: "groovy"

repositories {
    mavenCentral()
}

dependencies {
    // If using Spock, need to depend on geb-spock
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile("org.spockframework:spock-core:$spockVersion") {
        exclude group: "org.codehaus.groovy"
    }
    testCompile "org.yaml:snakeyaml:$yamlVersion"
    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"

    // If using JUnit, need to depend on geb-junit (3 or 4)
    testCompile "org.gebish:geb-junit4:$gebVersion"

    // Drivers
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-ie-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    testCompile("com.codeborne:phantomjsdriver:1.2.1") {
        // phantomjs driver pulls in a different selenium version
        transitive = false
    }
    testCompile "org.apache.commons:commons-lang3:3.0"
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        reports {
            html.destination = reporting.file("$name/tests")
            junitXml.destination = file("$buildDir/test-results/$name")
        }

        outputs.upToDateWhen { false }  // Always run tests

        systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
        systemProperty "geb.env", driver

        // If you wanted to set the baseUrl in your buildâ€¦
        // systemProperty "geb.build.baseUrl", "http://myapp.com"
    }
}